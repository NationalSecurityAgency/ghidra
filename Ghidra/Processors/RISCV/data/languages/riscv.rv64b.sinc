# RISC-V Zc & Zb Extensions (RV64 Specifics)
# NOTE: v1.0.0
# Reference: https://docs.riscv.org/reference/isa/unpriv/b-st-ext.html

# ==============================================================================
# Zba Instructions
# ==============================================================================

# 0000100 rs2 rs1 000 rd 0111011
:add.uw rd, rs1, rs2    is op2531=0x4 & rs2 & rs1 & op1214=0x0 & rd & op0006=0x3b
{
    rd = rs2 + zext(rs1:4);
}

# 0010000 rs2 rs1 010 rd 0111011
:sh1add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x2 & rd & op0006=0x3b
{
    rd = rs2 + (zext(rs1:4) << 1);
}

# 0010000 rs2 rs1 100 rd 0111011
:sh2add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x3b
{
    rd = rs2 + (zext(rs1:4) << 2);
}

# 0010000 rs2 rs1 110 rd 0111011
:sh3add.uw rd, rs1, rs2 is op2531=0x10 & rs2 & rs1 & op1214=0x6 & rd & op0006=0x3b
{
    rd = rs2 + (zext(rs1:4) << 3);
}

# 000010 shamt rs1 001 rd 0011011
:slli.uw rd, rs1, op2025 is op2631=0x2 & op2025 & rs1 & op1214=0x1 & rd & op0006=0x1b
{
    rd = zext(rs1:4) << op2025;
}

# ==============================================================================
# Zbb Instructions
# ==============================================================================

# 0110000 00010 rs1 001 rd 0011011
:cpopw rd, rs1          is op2531=0x30 & op2024=0x2 & rs1 & op1214=0x1 & rd & op0006=0x1b
{
    rd = zext(popcount(rs1:4));
}

# 0000100 00000 rs1 100 rd 0111011
:zext.h rd, rs1         is op2531=0x4 & op2024=0x0 & rs1 & op1214=0x4 & rd & op0006=0x3b
{
    rd = zext(rs1:2);
}

# ==============================================================================
# Zbs Instructions
# ==============================================================================

# 010010 shamt rs1 001 rd 0010011
:bclri rd, rs1, op2025  is op2631=0x12 & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
{
    rd = rs1 & ~(1:$(XLEN) << op2025);
}

# 010010 shamt rs1 101 rd 0010011
:bexti rd, rs1, op2025  is op2531=0x12 & op2025 & rs1 & op1214=0x5 & rd & op0006=0x13
{
    rd = (rs1 >> op2025) & 1;
}

# 011010 shamt rs1 001 rd 0010011
:binvi rd, rs1, op2025  is op2531=0x1a & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
{
    rd = rs1 ^ (1:$(XLEN) << op2025);
}

# 001010 shamt rs1 001 rd 0010011
:bseti rd, rs1, op2025  is op2531=0xa & op2025 & rs1 & op1214=0x1 & rd & op0006=0x13
{
    rd = rs1 | (1:$(XLEN) << op2025);
}

# ==============================================================================
# Zbkb Instructions
# ==============================================================================

# 011010 111000 rs1 101 rd 0010011
:rev8 rd, rs1           is op2031=0x6b8 & rs1 & op1214=0x5 & rd & op0006=0x13
{
    rd = ((rs1 & 0xFF) << 56) | ((rs1 & 0xFF00) << 40) |
         ((rs1 & 0xFF0000) << 24) | ((rs1 & 0xFF000000) << 8) |
         ((rs1 >> 8) & 0xFF000000) | ((rs1 >> 24) & 0xFF0000) |
         ((rs1 >> 40) & 0xFF00) | ((rs1 >> 56) & 0xFF);
}

# 0000100 rs2 rs1 100 rd 0111011
:packw rd, rs1, rs2     is op2531=0x4 & rs2 & rs1 & op1214=0x4 & rd & op0006=0x3b
{
    rd = sext(((zext(rs2:2) << 16) | zext(rs1:2)):4);
}

# 0110000 rs2 rs1 001 rd 0111011
:rolw rd, rs1, rs2      is op2531=0x30 & rs2 & rs1 & op1214=0x1 & rd & op0006=0x3b
{
    rd = sext((rs1:4 << (rs2 & 0x1F)) | (rs1:4 >> (32 - (rs2 & 0x1F))));
}

# 0110000 rs2 rs1 101 rd 0111011
:rorw rd, rs1, rs2      is op2531=0x30 & rs2 & rs1 & op1214=0x5 & rd & op0006=0x3b
{
    rd = sext( (rs1:4 >> (rs2 & 0x1F)) | (rs1:4 << (32 - (rs2 & 0x1F))) );
}

# 011000 shamt rs1 101 rd 0010011
:rori rd, rs1, op2025   is op2631=0x18 & op2025 & rs1 & op1214=0x5 & rd & op0006=0x13
{
    rd = (rs1 >> op2025) | (rs1 << (($(XLEN) << 3) - op2025));
}

# 0110000 shamt rs1 101 rd 0011011
:roriw rd, rs1, op2024  is op2531=0x30 & op2024 & rs1 & op1214=0x5 & rd & op0006=0x1b
{
    rd = sext((rs1:4 >> op2024) | (rs1:4 << (32 - op2024)));
}

# ==============================================================================
# Unimpl Instructions
# ==============================================================================

# 0110000 00001 rs1 001 rd 0011011
:ctzw rd, rs1           is op2531=0x30 & op2024=0x1 & rs1 & op1214=0x1 & rd & op0006=0x1b unimpl

# 0110000 00000 rs1 001 rd 0011011
:clzw rd, rs1           is op2531=0x30 & op2024=0x0 & rs1 & op1214=0x1 & rd & op0006=0x1b unimpl